buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.1.2'
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
        exclude(module: 'groovy')
    }

}

repositories {
    mavenCentral()
    jcenter()
}
group = 'com.github.kongpf8848.plugin'
version = '1.0.0'
def siteUrl = 'https://github.com/kongpf8848/VersionPlugin'
def gitUrl = 'https://github.com/kongpf8848/VersionPlugin.git'
def libraryDescription = 'Gradle Version Plugin'
def projectName = "version"
install {
    repositories.mavenInstaller {
        pom {
            project {
                groupId project.group
                artifactId archivesBaseName
                version project.version
                name projectName
                description libraryDescription
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name 'kongpf'
                        email 'kongpf8848@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = projectName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
